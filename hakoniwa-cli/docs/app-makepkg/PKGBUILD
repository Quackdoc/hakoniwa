# Maintainer: John Doe <johndoe@example.com>

pkgname=hakoniwa-cli
pkgver=1.1.3
pkgrel=1
pkgdesc="Process isolation for Linux using namespaces, resource limits, landlock and seccomp"
url=https://github.com/souk4711/hakoniwa
license=(GPL-3.0-only)
makedepends=(cargo)
depends=(libseccomp passt)
arch=(x86_64)
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate")
sha256sums=(SKIP)

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --frozen --release --all-features
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo test --frozen --all-features
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/hakoniwa"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
